bundle agent report_metadata(bundlename, metadata)
{
  vars:
      # pretty-print the authors and dependencies
      "dependencies" string => join(", ", "$(metadata)[depends]");
      "authors" string => join(", ", "$(metadata)[authors]");

  reports:
    cfengine::
      "$(bundlename): $($(metadata)[name]) license = $($(metadata)[license])";
      "$(bundlename): $($(metadata)[name]) dependencies = $(dependencies)";

      "$(bundlename): $($(metadata)[name]) version $($(metadata)[version]) by $(authors) starting up...";
}

bundle edit_line converge(marker, lines)
{
  vars:
      "regex" string => escape($(marker));

  delete_lines:
      "$(regex)" comment => "Delete lines matching the marker";
  insert_lines:
      "$(lines)" comment => "Insert the given lines";
}

bundle common env
{
  vars:
      "env" slist => { "LOGNAME", "PATH", "HOME" };
      "$(env)" string => getenv("$(env)", "128");
}

bundle edit_line prepend_if_no_line(string)
{
  insert_lines:
      "$(string)"
      location => dc_start,
      comment => "Prepend a line to the file if it doesn't already exist";
}

# same as COPBL start location body
body location dc_start
{
before_after => "before";
}
